grammar trabalho3.ReceitaHTML with org.eclipse.xtext.common.Terminals

generate receitaHTML "http://www.ReceitaHTML.trabalho3"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Programa: receitas+=Receita*;

Receita: 'receita' tipo=Classe '(' name=ID ')' img+=Imagem* corpo=Corpo;

Classe:
	'doce' | 'salgado'
;

Imagem:
	'img' '('name=STRING')'
;

Corpo:
	necessidades+=Necessidade+ modoDePreparo=ModoDePreparo
;

ModoDePreparo:
	'preparo' metodo+=Metodo+
;	

Necessidade:
	Ingrediente |
	Utensilio
;

Ingrediente:
	 '(' name=ID ',' quantidade=Float ',' tipo=UnidadeDeMedida ')'
;

Utensilio:
	'(' name=ID ')'
;
	
UnidadeDeMedida:
	'unidade' | 'ml' | 'mg' |
	'l' | 'g' | 'kg' |
	'colherDeSopa' | 'colherDeCha' |
	'xicaraDeCha'
;


Metodo:
	MetodoBater |
	MetodoUntar |
	MetodoAssar |
	MetodoMisturar |
	MetodoPicar |
	MetodoFritar
	;

MetodoBater:
	'bater' '(' usoDeIngrediente+=IngredienteASerUsado 
	(',' usoDeIngrediente+=IngredienteASerUsado)*
	',' tempo=Tempo ')' '->' subProduto=Ingrediente
;

IngredienteASerUsado:
	ingredientes=[Ingrediente] ':' quantidade=Float
;

MetodoUntar:
	'untar' '(' utensilio=[Utensilio] ','
	ingredientes=[Ingrediente] ')'
;

MetodoAssar:
	'assar' '(' usoDeIngrediente+=IngredienteASerUsado ',' tempo=Tempo ',' temperatura=Temperatura ')'
;

MetodoPicar:
	'picar' '(' usoDeIngrediente+=IngredienteASerUsado
	(',' usoDeIngrediente+=IngredienteASerUsado)* ')' '->' subProduto=Ingrediente
;

MetodoFritar:
	'fritar' '(' usoDeIngrediente+=IngredienteASerUsado
	(',' usoDeIngrediente+=IngredienteASerUsado)* ','
	tempo=Tempo ')'
;

MetodoMisturar:
	'misturar' '(' usoDeIngrediente+=IngredienteASerUsado
	(',' usoDeIngrediente+=IngredienteASerUsado)+ ')' '->' subProduto=Ingrediente
;

Tempo:
	quantidade=Float unidade=('min' | 'h' | 's' )
;

Temperatura:
	temp=Float medida=('c' | 'f' | 'k')
;

Float returns ecore::EFloat: INT? ('.' INT)?;
